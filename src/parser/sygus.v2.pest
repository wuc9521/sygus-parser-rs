//! A comment line starting with // ! means this impl is not standard
SyGuSProg = { SOI ~ SyGuSCmd+ ~ EOI}

// Auxiliary Lexical Categories
WhiteSpaceChar = { "\u{09}" | "\u{0A}" | "\u{0D}" | "\u{20}" } // rewrite internal WHITESPACE.
WHITESPACE     = _{ WhiteSpaceChar | "\t" | NEWLINE | COMMENT }

// ! additional

// 2.1 Comments
COMMENT = _{ ";" ~ (!NEWLINE ~ ANY)* }

// 2.2. Literals
Literal = { Numeral | Decimal | BoolConst | HexConst | BinConst | StringConst }
Numeral = @{"0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*}
Decimal = @{ Numeral ~ "." ~ "0"* ~ Numeral }
BoolConst = @{"true" | "false"}
HexConst = @{ "#x" ~ ASCII_HEX_DIGIT+ }
BinConst = @{ "#b" ~ ASCII_BIN_DIGIT+ }
StringConst = ${ "\"" ~ (RawString | "\\" ~ ANY)* ~ "\"" }
RawString = { (!("\\" | "\"") ~ ANY)+ }

// 2.3. Symbols
Symbol = @{ (ASCII_ALPHA | SpecialChar) ~ (ASCII_ALPHA | ASCII_DIGIT | SpecialChar)* } 
// ! In the standard version, reserved tokens are not removed
Keyword = { ":" ~ Symbol }
SpecialChar = @{ "+" | "-" | "/" | "*" | "=" | "%" | "?" | "!" | "." | "$" | "_" | "~" | "&" | "^" | "<" | ">" | "@" }

// 2.4 Identifier
Identifier = { 
  Symbol 
| "(" ~ "_" ~ Symbol ~ Index+ ~ ")" 
// ! In the standard version, underline cannot be ignored { "(" ~ "_" ~ Symbol ~ Index+ ~ ")" }
}
Index = { 
  Numeral 
| Symbol 
}

// 2.5 Attributes
Attribute = {
  Keyword 
| Keyword ~ AttributeValue
}

// ! borrowed from SMT-lib (not clearly defined in official syntax)
AttributeValue = { SpecConstant | Symbol | "(" ~ SExpr* ~ ")" }

// ! borrowed from SMT-lib (not defined in official syntax)
SExpr = { 
  SpecConstant 
| Symbol 
| Reserved 
| Keyword 
| "(" ~ SExpr* ~ ")"
}

// ! borrowed from SMT-lib (not clearly defined in official syntax)
SpecConstant  = { 
  Numeral 
| Decimal 
| HexConst
| BinConst
| StringConst
}


// 2.6 Sorts
Sort = {
  Identifier 
| "(" ~ Identifier ~ (Sort)+ ~ ")"
}

// 2.7 SyGuSTerms
SyGuSTerm = {
  Identifier
| Literal
| "(" ~ Identifier ~ SyGuSTerm+ ~ ")"
| "(" ~ "!" ~ SyGuSTerm ~ Attribute+ ~ ")" 
| "(" ~ "exists" ~ "(" ~ SortedVar+ ~ ")" ~ SyGuSTerm ~ ")"
| "(" ~ "forall" ~ "(" ~ SortedVar+ ~ ")" ~ SyGuSTerm ~ ")"
| "(" ~ "let" ~ "(" ~ VarBinding+ ~ SyGuSTerm ~ ")" ~ ")"
}

SyGuSBfTerm = {
  Identifier
| Literal
| "(" ~ Identifier ~ SyGuSBfTerm+ ~ ")"
| "(" ~ "!" ~ SyGuSBfTerm ~ Attribute+ ~ ")"
}

SortedVar = {  "(" ~ Symbol ~ Sort ~ ")"  }
VarBinding = { "(" ~ Symbol ~ SyGuSTerm ~ ")" }

// 2.8 Features
SyGuSFeature = {
  ":" ~ "grammars"
| ":" ~ "fwd-decls"
| ":" ~ "recursion"
| ":" ~ "oracles"
| ":" ~ "weights"
}

// 2.9 Commands
SyGuSCmd = {
  SyGuSCmdAssume
| SyGuSCmdCheckSynth
| SyGuSCmdChcConstraint
| SyGuSCmdConstraint
| SyGuSCmdDeclareVar
| SyGuSCmdDeclareWeight
| SyGuSCmdInvConstraint
| SyGuSCmdOptimizeSynth
| SyGuSCmdSetFeature
| SyGuSCmdSynthFun
// SyGuSCmdOracle =======================================================
| SyGuSCmdOracleAssume
| SyGuSCmdOracleConstraint
| SyGuSCmdDeclareOracleFun
| SyGuSCmdOracleConstraintIO
| SyGuSCmdOracleConstraintCex
| SyGuSCmdOracleConstraintMembership
| SyGuSCmdOracleConstraintPosWitness
| SyGuSCmdOracleConstraintNegWitness
| SyGuSCmdDeclareCorrectnessOracle
| SyGuSCmdDeclareCorrectnessCexOracle
// SyGuSCmdSMT  =======================================================
| SyGuSCmdDeclareDatatype
| SyGuSCmdDeclareDatatypes
| SyGuSCmdDeclareSort
| SyGuSCmdDefineFun
| SyGuSCmdDefineSort
| SyGuSCmdSetInfo
| SyGuSCmdSetLogic
| SyGuSCmdSetOption
//  ======================================================================

}

SyGuSCmdAssume = { "(" ~ #SyGuSTkAssume="assume" ~ SyGuSTerm ~ ")" }
SyGuSCmdCheckSynth = { "(" ~ #SyGuSTkCheckSynth="check-synth" ~ ")" }
SyGuSCmdChcConstraint = {"(" ~ #SyGuSTkChcConstraint="chc-constraint" ~ "(" ~ SortedVar* ~ ")" ~ SyGuSTerm ~ SyGuSTerm ~ ")" }
SyGuSCmdConstraint = { "(" ~ #SyGuSTkConstraint="constraint" ~ SyGuSTerm ~ ")" }
SyGuSCmdDeclareVar = { "(" ~ #SyGuSTkDeclareVar="declare-var" ~ Symbol ~ Sort ~ ")" }
SyGuSCmdDeclareWeight = { "(" ~ #SyGuSTkDeclareWeight="declare-weight" ~ Symbol ~ Attribute* ~ ")" }
SyGuSCmdInvConstraint = { "(" ~ #SyGuSTkInvConstaint="inv-constraint" ~ Symbol{4} ~ ")" }
SyGuSCmdOptimizeSynth = { "(" ~ #SyGuSTkOptimizeSynth="optimize-synth" ~ "(" ~ SyGuSTerm* ~ ")" ~ Attribute* ~ ")" }
SyGuSCmdSetFeature = {"(" ~ #SyGuSTkSetFeature="set-feature" ~ SyGuSFeature ~ BoolConst ~ ")" }
SyGuSCmdSynthFun = {"(" ~ #SyGuSTkSynthFun="synth-fun" ~ Symbol ~ "(" ~ SortedVar* ~ ")" ~ Sort ~ GrammarDef? ~ ")" }

SyGuSCmdOracleAssume = {"(" ~ #SyGuSTkOracleAssume="oracle-assume" ~ "(" ~ SortedVar* ~ ")" ~ "(" ~ SortedVar* ~ ")" ~ SyGuSTerm ~ Symbol ~ ")"}
SyGuSCmdOracleConstraint = {"(" ~ #SyGuSTkOracleConstraint="oracle-constraint" ~ "(" ~ SortedVar* ~ ")" ~ "(" ~ SortedVar* ~ ")" ~ SyGuSTerm ~ Symbol ~ ")"}
SyGuSCmdDeclareOracleFun = {"(" ~ #SyGuSTkDeclareOracleFun="declare-oracle-fun" ~ Symbol ~ "(" ~ Sort* ~ ")" ~ Sort ~ Symbol ~ ")"}
SyGuSCmdOracleConstraintIO = {"(" ~ #SyGuSTkOracleConstraintIO="oracle-constraint-io" ~ Symbol{2} ~ ")"}
SyGuSCmdOracleConstraintCex = {"(" ~ #SyGuSTkOracleConstraintCex="oracle-constraint-cex" ~ Symbol{2} ~ ")"}
SyGuSCmdOracleConstraintMembership = {"(" ~ #SyGuSTkOracleConstraintMembership="oracle-constraint-membership" ~ Symbol{2} ~ ")"}
SyGuSCmdOracleConstraintPosWitness = {"(" ~ #SyGuSTkOracleConstraintPoswitness="oracle-constraint-poswitness" ~ Symbol{2} ~ ")"}
SyGuSCmdOracleConstraintNegWitness = {"(" ~ #SyGuSTkOracleConstraintNegwitness="oracle-constraint-negwitness" ~ Symbol{2} ~ ")"}
SyGuSCmdDeclareCorrectnessOracle = {"(" ~ #SyGuSTkDeclareCorrectnessOracle="declare-correctness-oracle" ~ Symbol{2} ~ ")"}
SyGuSCmdDeclareCorrectnessCexOracle = {"(" ~ #SyGuSTkDeclareCorrectnessCexOracle="declare-correctness-cex-oracle" ~ Symbol{2} ~ ")"}


SyGuSCmdDeclareDatatype = { "(" ~ #SyGuSTkDeclareDatatype="declare-datatype" ~ Symbol ~ DTDecl ~ ")" }
SyGuSCmdDeclareDatatypes = { "(" ~ #SyGuSTkDeclareDatatypes="declare-datatypes" ~ "(" ~ SortDecl+ ~ ")" ~ "(" ~ DTDecl+ ~ ")" ~ ")" }
SyGuSCmdDeclareSort = { "(" ~ #SyGuSTkDeclareSort="declare-sort" ~ Symbol ~ Numeral ~ ")" }
SyGuSCmdDefineFun = { "(" ~ #SyGuSTkDefineFun="define-fun" ~ Symbol ~ "(" ~ SortedVar* ~ ")" ~ Sort ~ SyGuSTerm ~ ")" }
SyGuSCmdDefineSort = { "(" ~ #SyGuSTkDefineSort="define-sort" ~ Symbol ~ Sort ~ ")" }
SyGuSCmdSetInfo = { "(" ~ #SyGuSTkSetInfo="set-info" ~ Keyword ~ Literal ~ ")" }
SyGuSCmdSetLogic = { "(" ~ #SyGuSTkSetLogic="set-logic" ~ Symbol ~ ")" }
SyGuSCmdSetOption = { "(" ~ #SyGuSTkSetOption="set-option" ~ Keyword ~ Literal ~ ")" }

SortDecl = { "(" ~ Symbol ~ Numeral ~ ")" }
DTDecl = { "(" ~ DTConsDecl+ ~ ")" }
DTConsDecl = { "(" ~ Symbol ~ SortedVar* ~ ")" }
// ! In standard version, the first half of grammardef cannot be removed.
// ! But I see many implementations that allow this
// ! I guess this is a dialect or something.
GrammarDef = { ("(" ~ SortedVar+ ~ ")")? ~ "(" ~ GroupedRuleList+ ~ ")" }
GroupedRuleList = { "(" ~ Symbol ~ Sort ~ "(" ~ SyGuSGTerm+ ~ ")" ~ ")" }
SyGuSGTerm = {
  "(" ~ "Constant" ~ Sort ~ ")"
| "(" ~ "Variable" ~ Sort ~ ")"
| SyGuSBfTerm
}

Reserved = { ReservedGeneralWord | ReservedCommandName }
ReservedGeneralWord = { // 
  "!" | "_" | "as" | "BINARY" | "DECIMAL" | "exists" | "HEXADECIMAL" | "forall" | "let" | "match" | "NUMERAL" | "par" | "STRING"
}
ReservedCommandName = {
  "assert" 
| "check-sat" 
| "check-sat-assuming" 
| "declare-const" 
| "declare-datatype" 
| "declare-datatypes" 
| "declare-fun" 
| "declare-sort" 
| "declare-sort-parameter" 
| "define-const" 
| "define-fun" 
| "define-fun-rec" 
| "define-sort" 
| "echo" 
| "exit" 
| "get-assertions" 
| "get-assignment" 
| "get-info" 
| "get-model" 
| "get-option" 
| "get-proof" 
| "get-unsat-assumptions" 
| "get-unsat-core" 
| "get-value" 
| "pop" 
| "push" 
| "reset" 
| "reset-assertions" 
| "set-info" 
| "set-logic" 
| "set-option"
}

PredefinedSymbol = {  // Predefined symbols
  "Bool" 
| "continued-execution" 
| "error" 
| "false" 
| "immediate-exit" 
| "incomplete" 
| "logic" 
| "memout" 
| "sat" 
| "success" 
| "theory" 
| "true" 
| "unknown" 
| "unsupported" 
| "unsat"
}

PredefinedKeyword = { // Predefined keywords
  ":" ~ "all-statistics" 
| ":" ~ "assertion-stack-levels"
| ":" ~ "authors"
| ":" ~ "category"
| ":" ~ "chainable"
| ":" ~ "definition"
| ":" ~ "diagnostic-output-channel" 
| ":" ~ "error-behavior" 
| ":" ~ "extensions" 
| ":" ~ "funs" 
| ":" ~ "funs-description"
| ":" ~ "global-declarations" 
| ":" ~ "interactive-mode" 
| ":" ~ "language"
| ":" ~ "left-assoc" 
| ":" ~ "license" 
| ":" ~ "name" 
| ":" ~ "named"
| ":" ~ "notes" 
| ":" ~ "pattern" 
| ":" ~ "print-success" 
| ":" ~ "produce-assignments" 
| ":" ~ "produce-models" 
| ":" ~ "produce-proofs"
| ":" ~ "produce-unsat-assumptions" 
| ":" ~ "produce-unsat-cores" 
| ":" ~ "random-seed" 
| ":" ~ "reason-unknown"
| ":" ~ "regular-output-channel" 
| ":" ~ "reproducible-resource-limit" 
| ":" ~ "right-assoc" 
| ":" ~ "smt-lib-version"
| ":" ~ "sorts" 
| ":" ~ "sorts-description"
| ":" ~ "source" 
| ":" ~ "status" 
| ":" ~ "theories" 
| ":" ~ "values" 
| ":" ~ "verbosity" 
| ":" ~ "version"
}